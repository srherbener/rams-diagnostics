#!/usr/bin/env perl
#
# Script to run azavg on the set of diagnostics specified in the config file
#
# The config file defines the cases, time dirs and diagnostics that are to be run. Format
# for lines in the config file are:
#   Case: <CaseName>
#   TimeDir: <Dir> <TimeSpec>
#   Azavg: <Diagnostic> <Nbands> <wfilterMin> <WfilterMax>
#
#   <CaseName> corresponds to the simulation name such as TC_SEED_C0100 or SA0SC0.
#
#   <Dir> is a subdirectory of the <CaseName>/GRADS directory where the GRADS files live
#   <TimeSpec> is the revu time spec (in the TVAR(1) spec)
#
#   <Diagnositc> is the name of the azavg function
#   <Nbands> is the number of radial bands to divide the space into
#   <WfilterMin> is the lower limit on the w filter (take data points with w < WfilterMin>
#   <WfilterMax> is the upper limit on the w filter (take data points with w > WfilterMax>
#

BEGIN { push(@INC, "$ENV{HOME}/etc/perl"); }

use strict;
use File::Basename;
use DiagUtils;

#######################################################################
# SUBROUTINES
#######################################################################

#######################################################################
# CreateFileLists()
#
# This routine will take the case, time dir, and diagnostic name
# and create the file lists (input and output) needed for azavg.
#
sub CreateFileLists
  {
  my ($Case, $Tdir, $Adiag) = @_;
  
  my $InFiles;
  my $OutFiles;

  # InFiles is a colon separated list that specifies all the required input
  # GRADS files. All diagnostics need the variables 'w' and 'press' so that
  # filtering can be done, and the storm center (from sfc pressure) can be
  # found. The diagnostic name matches the variable name you want except
  # for the diagnostics 'speed_t' and 'speed_r' which both require 'u' and
  # 'v' data.
  # If the diagnostic is either of 'w' or 'press', then don't append
  # the $Adiag name on since we've already added 'w' and 'press' to the list.
  $InFiles = "";
  $InFiles = &AppendGradsVarFile($InFiles, $Case, $Tdir, "w");
  $InFiles = &AppendGradsVarFile($InFiles, $Case, $Tdir, "press");
  if (($Adiag eq 'speed_t') || ($Adiag eq 'speed_r'))
    {
    $InFiles = &AppendGradsVarFile($InFiles, $Case, $Tdir, "u");
    $InFiles = &AppendGradsVarFile($InFiles, $Case, $Tdir, "v");
    }
  elsif (($Adiag ne "w") && ($Adiag ne "press"))
    {
    $InFiles = &AppendGradsVarFile($InFiles, $Case, $Tdir, $Adiag);
    }
  
  # OutFiles is actually a prefix that gets ".ctl" and ".gra" appended to it
  # to form the output GRADS control and data files.
  $OutFiles = "AzAveragedData/" . ${Tdir} . "/" . ${Adiag} . "_" . ${Case};

  return ($InFiles, $OutFiles);
  }

#######################################################################
# AppendGradsVarFile()
#
# This routine will take the case, time dir, and diagnostic name
# and create the file lists (input and output) needed for azavg.
#
sub AppendGradsVarFile
  {
  my ($InFiles, $Case, $Tdir, $Var) = @_;

  my $InFileList;

  my $InFile;
  my @f;

  # use ls to find the complete file name
  $InFile = "";
  open (LS, "/bin/ls $Case/GRADS/$Tdir/$Var-*.ctl |") or die "Cannot execute ls: $!";
  while (<LS>)
    {
    if (/\.ctl/)
      {
      @f = split(' ');
      $InFile = $f[0];
      }
    }
  close(LS);

  # append the new file name to the list
  $InFileList = $InFiles;
  if ($InFileList eq "")
    {
    $InFileList = $InFile;
    }
  else
    {
    $InFileList = $InFileList . ":" . $InFile;
    }
 
  return $InFileList;
  }

#######################################################################
# MAIN
#######################################################################

my $ConfigFile;

my $Cases;
my $TimeDirs;
my $Vars;
my $AzavgDiags;
my $Diags;

my @f;
my $Case;
my $Tdir;
my $Adiag;
my $Nbands;
my $Wmin;
my $Wmax;
my $InFiles;
my $OutFiles;

my @SysArgs;
my $OutDir;

$ConfigFile = $ARGV[0];

# read in info from the config file
($Cases, $TimeDirs, $Vars, $AzavgDiags, $Diags) = &DiagUtils::ReadConfigFile($ConfigFile);

# make sure at least one of each of cases, time_directories and diagnostics has
# been specified
if ((! defined(%$Cases)) || (! defined(%$TimeDirs)) || (! defined(%$AzavgDiags)))
  {
  print STDERR "ERROR: must specify at least one of each of case, time directory and diagnostic\n";
  exit 1;
  }

# In all cases, except 'speed_t' and 'speed_r', <Diagnostic> matches the name of the
# variable that you want to average. Also, the variable name matches the prefix of the GRADS
# control file.
#
# Walk through all combinations of cases, time directories, and diagnostics.
foreach $Case (sort(keys(%$Cases)))
  {
  foreach $Tdir (sort(keys(%$TimeDirs)))
    {
    foreach $Adiag (sort(keys(%$AzavgDiags)))
      {
      $Nbands = $$AzavgDiags{$Adiag}{NBANDS};
      $Wmin = $$AzavgDiags{$Adiag}{WMIN};
      $Wmax = $$AzavgDiags{$Adiag}{WMAX};

      ($InFiles,$OutFiles) = &CreateFileLists($Case, $Tdir, $Adiag);

      print "***************************************************************\n";
      print "Running azavg:\n";
      print "  Case: $Case\n";
      print "  Time Directory: $Tdir\n";
      print "  Diagnostic: $Adiag\n";
      print "    Number Radial Bands: $Nbands\n";
      print "    W Filter Min: $Wmin\n";
      print "    W Filter Max: $Wmax\n";
      print "  Input File List: $InFiles\n";
      print "  Output File Prefix: $OutFiles\n";

      # run azavg, make sure the output directory exists
      $OutDir = dirname($OutFiles);
      if (! -d $OutDir)
        {
        @SysArgs = ("mkdir", "-p", $OutDir);
        system(@SysArgs);
        }

      @SysArgs = ("azavg", $InFiles, $OutFiles, $Nbands, $Wmin, $Wmax, $Adiag);
      system(@SysArgs);
      }
    }
  }

exit 0;
